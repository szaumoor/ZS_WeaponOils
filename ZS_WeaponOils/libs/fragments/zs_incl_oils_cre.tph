COPY_EXISTING ~UDPRINCE.CRE~ ~override~ // kuotoan prince
    ADD_CRE_ITEM ~ZSWOPS05~ #10 #0 #0 ~none~ ~inv~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~KUOLIE01.CRE~ ~override~ // kuotoan lieutenant
              ~UDKUO01.CRE~  ~override~ // kuotoan leader
    ADD_CRE_ITEM ~ZSWOPS05~ #3 #0 #0 ~none~ ~inv~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~KUOCAP01.CRE~ ~override~ // kuotoan captain (fighter)
              ~KUOCLE20.CRE~ ~override~ // kuotoan captain (cleric)
    ADD_CRE_ITEM ~ZSWOPS05~ #2 #0 #0 ~none~ ~inv~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~UDPHAE01.CRE~ ~override~ // phaere
              ~UDPHAE02.CRE~ ~override~ // phaere
              ~CUPHAER.CRE~ ~override~ // phaere
              ~UDSOLA01.CRE~ ~override~ // solaufein
              ~UDSOLA02.CRE~ ~override~ // solaufein
              ~DADROW10.CRE~ ~override~ // drow torturer
              ~DADROW11.CRE~ ~override~ // drow torturer
              ~DADROW8.CRE~  ~override~ // drow torturer
              ~DADROW9.CRE~  ~override~ // drow torturer
              ~DADROW6.CRE~  ~override~ // drow leader
    ADD_CRE_ITEM ~ZSWOPS02~ #2 #0 #0 ~none~ ~inv~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~bddrowas.CRE~ ~override~ // SOD drow assassin
    ADD_CRE_ITEM ~ZSWOPS02~ #5 #0 #0 ~none~ ~inv~
    ADD_CRE_ITEM ~ZSWOPS01~ #3 #0 #0 ~none~ ~inv~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~bmugg1.CRE~ ~override~
    ADD_CRE_ITEM ~ZSWOPS02~ #1 #0 #0 ~none~ ~inv~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~bdgass1.CRE~ ~override~
    ADD_CRE_ITEM ~ZSWOPS01~ #1 #0 #0 ~none~ ~inv~
    ADD_CRE_ITEM ~ZSWOPS02~ #1 #0 #0 ~none~ ~inv~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~CULTT3.CRE~ ~override~
    ADD_CRE_ITEM ~ZSWOPS01~ #2 #0 #0 ~none~ ~inv~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~SLYTHE.CRE~ ~override~
              ~KRYSTI.CRE~ ~override~ // KRYSTIN
    ADD_CRE_ITEM ~ZSWOPS01~ #2 #0 #0 ~none~ ~inv~
    ADD_CRE_ITEM ~ZSWOPS03~ #1 #0 #0 ~none~ ~inv~ // dragon bile
BUT_ONLY IF_EXISTS

COPY_EXISTING ~SEVPAT05.CRE~ ~override~
    ADD_CRE_ITEM ~ZSWOPS01~ #3 #0 #0 ~none~ ~inv~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~CHALCY02.CRE~ ~override~
    ADD_CRE_ITEM ~ZSWOPS01~ #5 #0 #0 ~none~ ~inv~
    ADD_CRE_ITEM ~ZSWOPS03~ #2 #0 #0 ~none~ ~inv~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~DRAGBLUE.CRE~ ~override~
              ~DRAGRED.CRE~  ~override~
              ~DRAGBLAC.CRE~ ~override~
              ~DRAGBROW.CRE~ ~override~
              ~DRAGPURP.CRE~ ~override~
              ~DRAGSILV.CRE~ ~override~
              ~DRAGYELL.CRE~ ~override~
              ~DRAGSHAD.CRE~ ~override~
              ~FIRKRA02.CRE~ ~override~
              ~UDSILVER.CRE~ ~override~
              ~BAZDRA02.CRE~ ~override~
    ADD_CRE_ITEM ~ZSWOPS03~ #5 #0 #0 ~unstealable~ ~inv~ // dragon bile
BUT_ONLY IF_EXISTS

COPY_EXISTING ~bdmorent.CRE~ ~override~
    ADD_CRE_ITEM ~ZSWOPS03~ #3 #0 #0 ~unstealable~ ~inv~ // dragon bile
BUT_ONLY IF_EXISTS

WITH_SCOPE BEGIN
    // distribution of oils to thieves
    OUTER_SET thief = IDS_OF_SYMBOL(class THIEF)
    OUTER_SET mage_thief = IDS_OF_SYMBOL(class MAGE_THIEF)
    OUTER_SET fighter_thief = IDS_OF_SYMBOL(class FIGHTER_THIEF)
    OUTER_SET cleric_thief = IDS_OF_SYMBOL(class CLERIC_THIEF)
    OUTER_SET fighter_mage_thief = IDS_OF_SYMBOL(class FIGHTER_MAGE_THIEF)
    OUTER_SET assasin =  IDS_OF_SYMBOL(kit ASSASIN)
    COPY_EXISTING_REGEXP ~^.*\.cre$~ ~override~
        READ_BYTE 0x273 class
        PATCH_IF class = thief              OR
                 class = mage_thief         OR
                 class = fighter_thief      OR
                 class = cleric_thief       OR
                 class = fighter_mage_thief
        BEGIN
            SET kit = SHORT_AT 0x246 | (SHORT_AT 0x244) << 16
            SET chance = RANDOM(1 100)
            PATCH_IF chance > (100 - zswo_thief_noxious_oil_assignment_chances) BEGIN
                SET amount = RANDOM(1 3)
                PATCH_IF kit = assasin BEGIN
                    amount = amount * 2
                END
                ADD_CRE_ITEM ~ZSWOPS01~ (amount) (0) (0) ~none~ ~inv~ // poison oil
                SET chance_drow_poison = RANDOM(1 100)
                PATCH_IF chance_drow_poison <= zswo_thief_drow_poison_assignment_chances BEGIN
                    ADD_CRE_ITEM ~ZSWOPS02~ (1) (0) (0) ~none~ ~inv~ // drow soporific oil
                END
            END ELSE PATCH_IF chance <= zswo_thief_caustic_oil_assignment_chances BEGIN
                SET amount = RANDOM(1 3)
                ADD_CRE_ITEM ~ZSWOAC01~ (amount) (0) (0) ~none~ ~inv~ // acid oil
            END
        END
    BUT_ONLY
END
