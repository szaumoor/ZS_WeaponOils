ACTION_IF zswo_allow_random_redistribution_cre BEGIN
    WITH_SCOPE BEGIN
        OUTER_SET thief           = IDS_OF_SYMBOL(class THIEF)
        OUTER_SET fighter_thief   = IDS_OF_SYMBOL(class FIGHTER_THIEF)
        OUTER_SET assasin         =  IDS_OF_SYMBOL(kit ASSASIN)

        OUTER_SET fighter         = IDS_OF_SYMBOL(class FIGHTER)
        OUTER_SET ranger          = IDS_OF_SYMBOL(class RANGER)

        OUTER_SET paladin_all     = IDS_OF_SYMBOL(class PALADIN_ALL)
        OUTER_SET blackguard      =  IDS_OF_SYMBOL(kit BLACKGUARD)
        OUTER_SET cleric          = IDS_OF_SYMBOL(class CLERIC)

        OUTER_SET mage            = IDS_OF_SYMBOL(class MAGE)
        OUTER_SET sorcerer        = IDS_OF_SYMBOL(class SORCERER)
        OUTER_SET mage_thief      = IDS_OF_SYMBOL(class MAGE_THIEF)
        OUTER_SET bard            = IDS_OF_SYMBOL(class BARD_ALL)
        OUTER_SET fighter_mage    = IDS_OF_SYMBOL(class FIGHTER_MAGE)

        OUTER_SET lawful_good     = IDS_OF_SYMBOL(alignmen LAWFUL_GOOD)
        OUTER_SET neutral_good    = IDS_OF_SYMBOL(alignmen NEUTRAL_GOOD)
        OUTER_SET chaotic_good    = IDS_OF_SYMBOL(alignmen CHAOTIC_GOOD)
        OUTER_SET lawful_neutral  = IDS_OF_SYMBOL(alignmen LAWFUL_NEUTRAL)
        OUTER_SET neutral         = IDS_OF_SYMBOL(alignmen NEUTRAL)
        OUTER_SET chaotic_neutral = IDS_OF_SYMBOL(alignmen CHAOTIC_NEUTRAL)
        OUTER_SET lawful_evil     = IDS_OF_SYMBOL(alignmen LAWFUL_EVIL)
        OUTER_SET neutral_evil    = IDS_OF_SYMBOL(alignmen NEUTRAL_EVIL)
        OUTER_SET chaotic_evil    = IDS_OF_SYMBOL(alignmen CHAOTIC_EVIL)
        COPY_EXISTING_REGEXP ~^.*\.cre$~ ~override~
            READ_ASCII 0x280 sname
            PATCH_IF (NOT FILE_CONTAINS_EVALUATED(~pdialog.2da~ ~%sname%~)) OR // i.e. if they're not companions
                    (FILE_EXISTS_IN_GAME ~bddialog.2da~ AND (NOT FILE_CONTAINS_EVALUATED(~bddialog.2da~ ~%sname%~)))
            BEGIN
                READ_BYTE 0x273 class
                PATCH_IF class = thief        OR
                         class = fighter_thief
                BEGIN
                    SET kit = SHORT_AT 0x246 | (SHORT_AT 0x244) << 16
                    SET chance = RANDOM(1 100)
                    PATCH_IF chance > (100 - zswo_thief_noxious_oil_assignment_chances) BEGIN
                        SET amount = RANDOM(1 3)
                        PATCH_IF kit = assasin BEGIN
                            amount = amount * 2
                        END
                        ADD_CRE_ITEM ~ZSWOPS01~ (amount) (0) (0) ~none~ ~inv~ // poison oil
                        SET chance_drow_poison = RANDOM(1 100)
                        PATCH_IF chance_drow_poison <= zswo_thief_drow_poison_assignment_chances BEGIN
                            ADD_CRE_ITEM ~ZSWOPS02~ (1) (0) (0) ~none~ ~inv~ // drow soporific oil
                        END
                    END ELSE PATCH_IF chance <= zswo_thief_caustic_oil_assignment_chances BEGIN
                        ADD_CRE_ITEM ~ZSWOAC01~ (amount) (0) (0) ~none~ ~inv~ // acid oil
                    END
                END

                ELSE PATCH_IF class = fighter OR
                              class = ranger
                BEGIN
                    SET kit = SHORT_AT 0x246 | (SHORT_AT 0x244) << 16
                    SET chance = RANDOM(1 100)
                    PATCH_IF chance <= zswo_fighter_elemental_assignment_chances BEGIN
                        SET oil_type = RANDOM(1 3)
                        SET amount = RANDOM(1 2)
                        PATCH_IF oil_type = 1 BEGIN
                            ADD_CRE_ITEM ~ZSWOFR01~ (amount) (0) (0) ~none~ ~inv~
                        END ELSE PATCH_IF oil_type = 2 BEGIN
                            ADD_CRE_ITEM ~ZSWOCD01~ (amount) (0) (0) ~none~ ~inv~
                        END ELSE PATCH_IF oil_type = 3 BEGIN
                            ADD_CRE_ITEM ~ZSWOEL01~ (amount) (0) (0) ~none~ ~inv~
                        END
                    END
                END

                ELSE PATCH_IF class = cleric      OR
                              class = paladin_all
                BEGIN
                    READ_BYTE 0x27b alignment
                    PATCH_IF alignment = chaotic_evil OR // PROFANE
                             alignment = neutral_evil OR
                             alignment = lawful_evil
                    BEGIN

                        SET kit = SHORT_AT 0x246 | (SHORT_AT 0x244) << 16
                        PATCH_IF kit = blackguard BEGIN // poison OR profane
                            SET chance = RANDOM(1 100)
                            PATCH_IF chance <= 50 BEGIN
                                ADD_CRE_ITEM ~ZSWOPS01~ (1) (0) (0) ~none~ ~inv~
                            END
                        END

                        SET chance = RANDOM(1 100)
                        PATCH_IF chance <= 20 BEGIN
                            ADD_CRE_ITEM ~ZSWOUL01~ (1) (0) (0) ~none~ ~inv~
                        END

                    END ELSE PATCH_IF alignment = chaotic_neutral OR // random chance of profane or sanctified
                                      alignment = neutral         OR
                                      alignment = lawful_neutral
                    BEGIN

                        SET chance = RANDOM(1 100)
                        PATCH_IF chance <= 50 BEGIN
                            SET chance = RANDOM(1 100)
                            PATCH_IF chance <= 15 BEGIN
                                ADD_CRE_ITEM ~ZSWOHL01~ (1) (0) (0) ~none~ ~inv~
                            END
                        END ELSE BEGIN
                            SET chance = RANDOM(1 100)
                            PATCH_IF chance <= 15 BEGIN
                                ADD_CRE_ITEM ~ZSWOUL01~ (1) (0) (0) ~none~ ~inv~
                            END
                        END

                    END ELSE PATCH_IF alignment = chaotic_good OR // sanctified
                                      alignment = neutral_good OR
                                      alignment = lawful_good
                    BEGIN
                        SET chance = RANDOM(1 100)
                        PATCH_IF chance <= 20 BEGIN
                            ADD_CRE_ITEM ~ZSWOHL01~ (1) (0) (0) ~none~ ~inv~
                        END
                    END

                END

                ELSE PATCH_IF class = mage         OR
                              class = sorcerer     OR
                              class = bard         OR
                              class = fighter_mage OR
                              class = mage_thief
                BEGIN
                    SET chance = RANDOM(1 100)
                    PATCH_IF chance <= zswo_mage_arcane_oil_assignment_chances BEGIN
                        ADD_CRE_ITEM ~ZSWOMD01~ (1) (0) (0) ~none~ ~inv~
                    END
                END
            END
        BUT_ONLY
    END
END
